@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

set(CMAKE_THREAD_PREFER_PTHREAD TRUE)
set(THREADS_PREFER_PTHREAD_FLAG TRUE)
find_dependency(Threads)

set(_CMAKE_MODULE_PATH_OLD "${CMAKE_MODULE_PATH}")
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

set(VAST_ENABLE_ASSERTIONS "@VAST_ENABLE_ASSERTIONS@")
set(VAST_ENABLE_BUNDLED_CAF "@VAST_ENABLE_BUNDLED_CAF@")
set(VAST_ENABLE_RELOCATABLE_INSTALLATIONS
    "@VAST_ENABLE_RELOCATABLE_INSTALLATIONS@")
set(VAST_ENABLE_UNIT_TESTS "@VAST_ENABLE_UNIT_TESTS@")
set(VAST_CMAKE_BUILD_TYPE "@CMAKE_BUILD_TYPE@")
set(VAST_CMAKE_CONFIGURATION_TYPES "@CMAKE_CONFIGURATION_TYPES@")
if (NOT DEFINED CAF_ROOT_DIR)
  set(CAF_ROOT_DIR "@CAF_ROOT_DIR@")
endif ()
if (NOT DEFINED BUILD_SHARED_LIBS)
  set(BUILD_SHARED_LIBS "@BUILD_SHARED_LIBS@")
endif ()

include(VASTMacDependencyPaths)
include(VASTRegisterPlugin)

find_package(Flatbuffers CONFIG)
if (NOT Flatbuffers_FOUND)
  find_package(FlatBuffers CONFIG)
  if (NOT FlatBuffers_FOUND)
    message(FATAL_ERROR "Flatbuffers is required but can't be found.")
  endif ()
endif ()

@VAST_FIND_DEPENDENCY_LIST@

set(CMAKE_MODULE_PATH "${_CMAKE_MODULE_PATH_OLD}")
unset(_CMAKE_MODULE_PATH_OLD)

@VAST_EXTRA_TARGETS_FILES@
include("${CMAKE_CURRENT_LIST_DIR}/VASTTargets.cmake")

if (NOT @PROJECT_NAME@_FIND_QUIETLY)
  message(
    STATUS "Found VAST (${VAST_CMAKE_BUILD_TYPE}): ${CMAKE_CURRENT_LIST_DIR}")
endif ()
