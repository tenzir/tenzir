@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

set(CMAKE_THREAD_PREFER_PTHREAD TRUE)
set(THREADS_PREFER_PTHREAD_FLAG TRUE)
find_dependency(Threads)

set(_CMAKE_MODULE_PATH_OLD "${CMAKE_MODULE_PATH}")
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

set(TENZIR_ENABLE_ASSERTIONS "@TENZIR_ENABLE_ASSERTIONS@")
set(TENZIR_ENABLE_BUNDLED_CAF "@TENZIR_ENABLE_BUNDLED_CAF@")
set(TENZIR_ENABLE_RELOCATABLE_INSTALLATIONS
    "@TENZIR_ENABLE_RELOCATABLE_INSTALLATIONS@")
set(TENZIR_ENABLE_UNIT_TESTS "@TENZIR_ENABLE_UNIT_TESTS@")
set(TENZIR_CMAKE_BUILD_TYPE "@CMAKE_BUILD_TYPE@")
string(TOUPPER "${TENZIR_CMAKE_BUILD_TYPE}" _TENZIR_CMAKE_BUILD_TYPE_UPPERCASE)
set(TENZIR_CMAKE_CONFIGURATION_TYPES "@CMAKE_CONFIGURATION_TYPES@")
if (NOT DEFINED CAF_ROOT_DIR)
  set(CAF_ROOT_DIR "@CAF_ROOT_DIR@")
endif ()
if (NOT DEFINED BUILD_SHARED_LIBS)
  set(BUILD_SHARED_LIBS "@BUILD_SHARED_LIBS@")
endif ()

include(TenzirMacDependencyPaths)
include(TenzirRegisterPlugin)
include(TenzirUtilities)

find_package(Flatbuffers CONFIG)
if (NOT Flatbuffers_FOUND)
  find_package(FlatBuffers CONFIG)
  if (NOT FlatBuffers_FOUND)
    message(FATAL_ERROR "Flatbuffers is required but can't be found.")
  endif ()
endif ()

@TENZIR_FIND_DEPENDENCY_LIST@

set(CMAKE_MODULE_PATH "${_CMAKE_MODULE_PATH_OLD}")
unset(_CMAKE_MODULE_PATH_OLD)

@TENZIR_EXTRA_TARGETS_FILES@
include("${CMAKE_CURRENT_LIST_DIR}/TenzirTargets.cmake")

get_target_property(TENZIR_PREFIX_DIR tenzir::tenzir
                    "IMPORTED_LOCATION_${_TENZIR_CMAKE_BUILD_TYPE_UPPERCASE}")
get_filename_component(TENZIR_PREFIX_DIR "${TENZIR_PREFIX_DIR}" DIRECTORY)
get_filename_component(TENZIR_PREFIX_DIR "${TENZIR_PREFIX_DIR}" DIRECTORY)

if (NOT @PROJECT_NAME@_FIND_QUIETLY)
  message(
    STATUS
      "Found Tenzir (${TENZIR_CMAKE_BUILD_TYPE}): ${CMAKE_CURRENT_LIST_DIR}")
endif ()
